import com.iteego.glasir.build.tasks.*

def moduleWithFlavors(String module) {
  String result = module
  if (project.hasProperty('flavor')) {
    result += ".${project.flavor}"
  }

  result
}

configurations { 
  ciReleaseEar
} 

dependencies {
  ciReleaseEar("${project.group}:${project.name}:${project.version}@ear") { changing = true }
}

task ciDeploy {
  description = "Deploys the release ear to jboss"
  dependsOn = [syncStore, syncAdmin]
}

//This is run only on ci server and _only_ if we are doing a deploy
//i.e. not when we are building/publishing the ear
gradle.taskGraph.whenReady {taskGraph ->
  if (taskGraph.hasTask(ciDeploy)) {
    println "> ========================================="
    println "> Resolving release ear from artifactory..."
    def earFromArtifactory = configurations.ciReleaseEar.find { it.name.endsWith(".ear") }
    println "> Resolved: $earFromArtifactory"
    
    //replace the bigEar variable with a pointer to the ear downloaded from 
    //artifactory into our gradle cache
    project.properties['bigEar'] = earFromArtifactory 
  } 
}

task startCiStore(type: RunAtgOnJBoss, dependsOn: [startDatabase]) {
  jBossHome     jBossRoot
  serverName    storeServerName
  modules       moduleWithFlavors("env.Main.store.dev")
  async         true
  description = "Starts the Ci Store instance"
}

task verifyCiStore(type: Tail, dependsOn: [startCiStore]) {
  file         "$jBossRoot/server/$storeServerName/log/console.log"
  failOn       regex: "ERROR[ ]*\\[", postFireTailPeriod: "5 seconds"
  succeedOn    "INFO.*JBoss \\(Microcontainer\\) \\[.*\\] Started in"
  timeoutAfter "5 minutes"
  description = "Starts the Ci Store instance, tails its output log and ensures there are no errors"
}

task startCiAdmin(type: RunAtgOnJBoss, dependsOn: []) {
  jBossHome     jBossRoot
  serverName    adminServerName
  modules       moduleWithFlavors("env.Main.admin.dev")
  async         true
  description = "Starts the Ci Admin instance"
}

task verifyCiAdmin(type: Tail, dependsOn: [startCiAdmin]) {
  file         "$jBossRoot/server/$adminServerName/log/console.log"
  failOn       regex: "ERROR[ ]*\\[", postFireTailPeriod: "5 seconds"
  succeedOn    "INFO.*JBoss \\(Microcontainer\\) \\[.*\\] Started in"
  timeoutAfter "5 minutes"
  description = "Starts the Ci Admin instance, tails its output log and ensures there are no errors"
}

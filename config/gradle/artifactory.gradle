import org.apache.tools.ant.filters.ReplaceTokens

configurations { 
  glasirEar {
    description = "Configuration for ear publishing on CI server"
  }
}

//we include the maven plugin to stop the build from logging warnings
apply plugin: 'maven' 
apply plugin: 'artifactory'
artifactory {
  contextUrl = artifactoryReaderUrl
  
  //the artifactoryDeployer, etc variables come from <userhome>/.gradle/gradle.properties  
  publish {
    defaults { 
      publishBuildInfo = true
      publishArtifacts = true
      publishPom = true      
      publishIvy = false    
      
      publishConfigs 'glasirEar'
      mavenDescriptor = "$buildDir/poms/pom-ear.xml"
    }

    repository {
      repoKey = 'glasir-snapshots-local'
      username = artifactoryDeployer
      password = artifactoryDeployerPwd
    }
  }
  resolve {
    repository {
      repoKey = 'glasir'
      username = artifactoryReader
      password = artifactoryReaderPwd      
    }
  }     
}   

task createPomDir(type: Directory) {
  dir = file("$buildDir/poms")
}

task generateEarPom(type: Copy, dependsOn: createPomDir) {
  from("$rootDir/config/artifact") {
    include "pom-ear.xml"
    filter(ReplaceTokens, tokens:[groupToken: project.group, 
                                  nameToken: project.name, 
                                  versionToken: project.version])
  }
  into "$buildDir/poms"
}

artifactoryPublish.dependsOn(generateEarPom)

artifacts {
	glasirEar(bigEar) { 
	  name = "glasir.commerce"	
	}
}

uploadArchives {
  uploadDescriptor = true	
}


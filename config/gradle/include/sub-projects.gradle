/******************************************************************************
 * Gradle project and sub-project setup calls
 *****************************************************************************/

apply plugin: 'groovy'

//make the clean task symlink-safe
task clean(overwrite: true) << {
  glasir.deleteDir "$rootDir/build"
}
def javaTasks = ['compileJava', 'jar','processResources','classes','compileTestJava','processTestResources',
 'testClasses', 'build', 'test','check', 'assemble']
 
javaTasks.each {
  //tasks.getByName(it).enabled = false 
}

//gradle.projectsEvaluated { g ->
//  tasks.each { task -> 
//    task.dependsOn(getTasksByName(task.name, true) - task)
//  }
//  javaTasks.each {
//    def t = tasks.getByName(it)
//    t.dependsOn(getTasksByName(it, true) - t)
//  }
//}

//the below is a list of test drivers for the geb web testing framework.
//a driver can emulate a specific browser and excercise the application UI.
//the htmlunit driver is a headless driver suitable for CI testing of the application UI
//See http://www.gebish.org/manual/current/configuration.html#driver_implementation
//for more details
//available drivers: htmlunit, firefox, ie, chrome, android                
ext.drivers = ['htmlunit']

allprojects {  
  project.ext.isBuildProject = file("$projectDir/src").isDirectory()
  if (!project.isBuildProject) {    
    return
  }
  
  apply plugin: 'groovy'

  repositories {
    maven {
      credentials.username artifactoryReader
      credentials.password artifactoryReaderPwd
      url "$artifactoryReaderUrl/glasir"
    }
  }

  dependencies {
    groovy "org.codehaus.groovy:groovy:1.7.10"

    testCompile "junit:junit:4.8.2", 
                "org.spockframework:spock-core:0.5-groovy-1.7",
                "org.codehaus.geb:geb-spock:0.6.1",
                "org.codehaus.geb:geb-junit4:0.6.1"

    drivers.each { driver ->
      testCompile "org.seleniumhq.selenium:selenium-${driver}-driver:2.9.0"
    }

    testRuntime "org.seleniumhq.selenium:selenium-support:2.9.0"
  }
  
  sourceCompatibility = "1.6"
  targetCompatibility = "1.6"
  
  compileJava {
    options.compilerArgs = ['-Xlint:deprecation'] //, '-Xlint:unchecked']
  }

  drivers.each { driver -> 
    task "${driver}Test"(type: Test) {
      //force geb tests to run even when executing repeated builds
      outputs.upToDateWhen { 
        def gebSpecs = fileTree(dir: project.projectDir, include: '**/*GebSpec.class')
        //if we have geb specs in this project, consider the htmlunitTest task and its siblings
        //never "up to date" forcing the geb tests to run every time the task is called
        return !(gebSpecs.files)
      }

      ext.isWebTest = true
      include '**/*GebSpec.class'
      
      
      testReportDir = file("${reporting.baseDir}/geb-tests")
      testResultsDir = file("$buildDir/geb-test-results/")
  
      systemProperty "geb.build.reportsDir", "${reporting.baseDir}/$name/geb"
      systemProperty "geb.env", driver
      
      //TODO: Externalize the below host/port configuration somehow
      systemProperty "geb.build.baseUrl", 'http://localhost:10080'
      
      reportLocationLogged = false
      afterTest { 
        if (didWork && !reportLocationLogged) {
          println "Geb functional test report written to ${testReportDir}.."
          reportLocationLogged = true
        }
      }
    }  
  }

  jar  {
    //only create a jar file if we have some classes to jar up, 
    //to to this we check if the 'compileJava' task did any work (if so, we have something
    //to jar up). TODO: if we need to create resources-only jars, this needs changing
    onlyIf {
      compileJava.didWork || processResources.didWork || compileGroovy.didWork
    }

    // Change where the resulting jar will end up
    destinationDir = file("lib")
    baseName = 'classes'
  }

  test {
    exclude '**/*GebSpec.class'
  }

  clean << {
    delete file: 'lib/classes.jar'
    if (!file('lib').listFiles()) {
      glasir.deleteDir file('lib')
    }
  }  
}

subprojects {
  if (!project.isBuildProject) return

  apply plugin: 'glasir.build'

  group = glasir.projectGroup

  //configures dependencies for this project based on the atg MANIFEST.MF file
  //ATG-Required attribute values. Applies these dependencies to the configurations
  //given as the second argument
  glasir {
    atgDependencies {
      applyToConfigurations "compile", "testCompile"      
      verbose false
    }     
  }
}

task aggregateTestReports(type: TestReportAggregator) {
  def buildDir = file("$projectDir/build")
  def reportsDir = file("$buildDir/reports")
  reportsDir.mkdirs()
  
  //dependsOn getTasksByName('check', true)

  includeWebTests = false
  testReportDir = file("${reportsDir}/tests")
  testResultsDir = file("${buildDir}/test-results")
  projects = subprojects
  doLast {
    if (didWork) {
      println "> "
      println "> Aggregated test report saved at $testReportDir/index.html"
      println "> "
    }
  }    
}
build.dependsOn(aggregateTestReports)

class TestReportAggregator extends Copy {
  def projects
  File testResultsDir
  boolean includeWebTests = false
  
  @OutputDirectory
  File testReportDir
  
  def TestReportAggregator() {
    dependsOn { testTasks }
    from { inputTestResultDirs }
    into { testResultsDir }
    outputs.upToDateWhen { 
      //println "AGGREGATE DEPENDSUPTODATE: ${dependsOnTaskDidWork()}"
      //println "INPUTS: ${inputTestResultDirs}"
      !dependsOnTaskDidWork()
    }
  }

  @TaskAction
  def aggregate() {
    def report = new org.gradle.api.internal.tasks.testing.junit.report.DefaultTestReport(testReportDir: testReportDir, testResultsDir: testResultsDir)
    report.generateReport()
  }

  def getTestTasks() {
    def testTasks = projects.collect { it.tasks.withType(Test) }.flatten()
    
    //exclude web tests from the 
    includeWebTests ? testTasks : testTasks.findAll { !it.hasProperty('isWebTest') } 
  }

  def getInputTestResultDirs() {
    testTasks*.testResultsDir
  }
}
